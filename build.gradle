buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.2.3'
        classpath 'me.tatarka:gradle-retrolambda:3.2.5'
        classpath 'me.tatarka.retrolambda.projectlombok:lombok.ast:0.2.3.a2'
    }

    // Exclude the version that the android plugin depends on.
    configurations.classpath.exclude group: 'com.android.tools.external.lombok'
}

allprojects {
    repositories {
        jcenter()
        mavenCentral()
    }
}

apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'

android {
    compileSdkVersion 22
    buildToolsVersion "23.0.2"

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        applicationId "nu.staldal.djdplayer"
        targetSdkVersion 22

        dependencies {
            compile 'com.android.support:appcompat-v7:22.2.1'
        }
    }

    if (project.hasProperty("KEY_STORE")) {
        signingConfigs {
            release {
                storeFile file(KEY_STORE)
                storePassword KEY_STORE_PASSWORD
                keyAlias KEY_ALIAS
                keyPassword KEY_PASSWORD
            }
        }
    } else {
        signingConfigs {
            release {
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard.pro'
            debuggable false
            signingConfig signingConfigs.release
        }
        preview {
            debuggable true
            signingConfig signingConfigs.release
        }
        debug {
            debuggable true
        }
    }

    productFlavors {
        mobile {
            minSdkVersion 16
        }
        tv {
            minSdkVersion 21

            dependencies {
                tvCompile 'com.android.support:leanback-v17:22.2.1'
            }
        }
    }

}
